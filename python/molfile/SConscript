Import('msysenv')
Import('libmsys')
import sys
env=msysenv.Clone()

cxx_flags = '' if sys.platform.startswith("win32") else '-Wno-unused-local-typedefs'

env.Append( 
        CPPPATH=['../../src', '../'],
        CXXFLAGS=cxx_flags,
        LIBS=['msys', 'boost_python$MSYS_BOOST_SUFFIX'])

if sys.platform != "win32":
    env.Append(CCFLAGS='-Wno-unused-function'.split())
else:
    env.Append(LIBS=['python27.lib'])
if env['PLATFORM']=='darwin':
    env.Append(CCFLAGS='-Wno-error')
elif sys.platform.startswith("linux"):
    env.Append(CCFLAGS='-Wno-cpp')

env.Append(CPPPATH='..')
ext=env.AddPythonExtension('_molfile', env.Split('''
plugin.cxx
reader.cxx
atom.cxx
frame.cxx
writer.cxx
molfilemodule.cxx
'''), prefix='msys/molfile')
env.Requires(ext, libmsys)

ext=env.AddPythonExtension('findframe','findframemodule.cxx',
        prefix='msys/molfile')
env.Requires(ext, libmsys)

env.AddPythonModule('__init__.py',prefix='msys/molfile')

