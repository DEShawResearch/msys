Import('env')
env=env.Clone()

opts=Variables()
opts.Add("MSYS_WITHOUT_ATOMSEL", "without atom selections?")
opts.Add("MSYS_WITHOUT_MAE", "without mae file support?")
opts.Update(env)

env.Append(LIBS=[
    'boost_iostreams', 'boost_filesystem', 'boost_system', 'boost_thread'])
env.Append(CPPPATH=['$OBJDIR/src'])

def build_version_hxx( target, source, env ):
    import os
    g=dict()
    execfile(source[0].path, g)
    verstr=g['msys_version']
    dash = verstr.find('-')
    if dash != -1:
        nice_verstr = verstr[:dash]
    else:
        nice_verstr = verstr
    major, minor, micro = nice_verstr.split('.')
    fd=file(target[0].path, 'w')
    print >> fd, "#define MSYS_VERSION \"%s\"" % verstr
    for x in 'major', 'minor', 'micro':
        u=x.upper()
        v=int(locals()[x])
        print >> fd, "#define MSYS_%s_VERSION %d" % (u,v)
    print >> fd, "#define MSYS_VERSION_HEX ((MSYS_MAJOR_VERSION << 16) | \\"
    print >> fd, "                          (MSYS_MINOR_VERSION <<  8) | \\"
    print >> fd, "                          (MSYS_MICRO_VERSION <<  0))"

    fd.close()

env.Command('version.hxx', 'version.py', build_version_hxx )

if env.get("MSYS_WITHOUT_ATOMSEL"):
    selenv=env.Clone()
    selenv.Append(CPPDEFINES=['MSYS_WITHOUT_ATOMSEL'])
    selobjs = selenv.AddObject("atomsel.cxx")
else:
    selobjs = env.AddObject(Split('''
atomsel.cxx
atomsel/expression.cxx
atomsel/k-nearest.cxx
atomsel/keyword.cxx
atomsel/keyword_predicate.cxx
atomsel/msys_keyword.cxx
atomsel/selection.cxx
atomsel/predicate.cxx
atomsel/regex.cxx
atomsel/vmd.cxx
atomsel/vmd_keyword.cxx
atomsel/VmdLexer.c
atomsel/VmdParser.c
atomsel/within_predicate.cxx
'''))

if env.get("MSYS_WITHOUT_MAE"):
    maeenv=env.Clone()
    maeobjs=maeenv.AddObject('mae/stub.cxx')
else:
    maeobjs=env.AddObject(Split('''
mae/ff.cxx
mae/export_mae.cxx
mae/import_mae.cxx
mae/mae.cxx
mae/maeatoms.cxx
mae/sitemap.cxx
mae/vdwmap.cxx

mae/ff/angles.cxx
mae/ff/bonds.cxx
mae/ff/cmap.cxx
mae/ff/constraints.cxx
mae/ff/dihedrals.cxx
mae/ff/dihedrals6atom.cxx
mae/ff/exclusions.cxx
mae/ff/inplanewags.cxx
mae/ff/pairs.cxx
mae/ff/pseudopol.cxx
mae/ff/restraints.cxx
mae/ff/vdwtypes.cxx
mae/ff/virtuals.cxx

mae/destro/Attribute.cxx         
mae/destro/DestroArray.cxx       
mae/destro/DestroBlock.cxx       
mae/destro/Destro.cxx            
mae/destro/DestroNamedBlock.cxx  
mae/destro/DestroRow.cxx
mae/destro/DestroTop.cxx
mae/destro/Maeff.cxx
mae/destro/prep_alchemical_mae.cxx
mae/destro/Tokenizer.cxx
mae/destro/Zing.cxx
'''))

libmsys=env.AddLibrary('msys', selobjs + maeobjs + Split('''

amber/import_crd.cxx
amber/import_prmtop.cxx

dms/dms.cxx
dms/export_dms.cxx
dms/import_dms.cxx

elements.cxx

load.cxx

pdb/pdb.cxx

schema.cxx
schema/schema.cxx

append.cxx
clone.cxx
override.cxx
param_table.cxx
provenance.cxx
system.cxx
term_table.cxx
value.cxx

'''))

env.AddStagedHeaders(Glob("*.hxx"), prefix='msys')

Export("libmsys")
