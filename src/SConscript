Import('env')
env=env.Clone()
env.Append(LIBS=['pcre', 'boost_iostreams', 'boost_filesystem', 'boost_system'])
env.Append(CPPPATH=['$OBJDIR/src'])

def build_version_hxx( target, source, env ):
    import os
    verstr=os.environ['MSYS_VERSION']
    major, minor, micro = verstr.split('.')
    fd=file(target[0].path, 'w')
    print >> fd, "#define MSYS_VERSION \"%s\"" % verstr
    for x in 'major', 'minor', 'micro':
        u=x.upper()
        v=int(locals()[x])
        print >> fd, "#define MSYS_%s_VERSION %d" % (u,v)
    print >> fd, "#define MSYS_VERSION_HEX ((MSYS_MAJOR_VERSION << 16) | \\"
    print >> fd, "                          (MSYS_MINOR_VERSION <<  8) | \\"
    print >> fd, "                          (MSYS_MICRO_VERSION <<  0))"

    fd.close()

env.Command('version.hxx', '#desres-install-helper', build_version_hxx )

libmsys=env.AddLibrary('msys', Split('''
dms/dms.cxx
dms/export_dms.cxx
dms/import_dms.cxx

json/export_json.cxx

mae/ff.cxx
mae/export_mae.cxx
mae/import_mae.cxx
mae/mae.cxx
mae/maeatoms.cxx
mae/sitemap.cxx
mae/vdwmap.cxx

mae/ff/angles.cxx
mae/ff/bonds.cxx
mae/ff/cmap.cxx
mae/ff/constraints.cxx
mae/ff/dihedrals.cxx
mae/ff/dihedrals6atom.cxx
mae/ff/exclusions.cxx
mae/ff/inplanewags.cxx
mae/ff/pairs.cxx
mae/ff/pseudopol.cxx
mae/ff/restraints.cxx
mae/ff/vdwtypes.cxx
mae/ff/virtuals.cxx

mae/destro/Attribute.cxx         
mae/destro/DestroArray.cxx       
mae/destro/DestroBlock.cxx       
mae/destro/Destro.cxx            
mae/destro/DestroNamedBlock.cxx  
mae/destro/DestroRow.cxx
mae/destro/DestroTop.cxx
mae/destro/Maeff.cxx
mae/destro/prep_alchemical_mae.cxx
mae/destro/Tokenizer.cxx
mae/destro/Zing.cxx

schema.cxx
schema/schema.cxx

append.cxx
clone.cxx
param_table.cxx
provenance.cxx
system.cxx
term_table.cxx
value.cxx

atomsel.cxx
atomsel/expression.cxx
atomsel/k-nearest.cxx
atomsel/keyword.cxx
atomsel/keyword_predicate.cxx
atomsel/msys_keyword.cxx
atomsel/selection.cxx
atomsel/predicate.cxx
atomsel/regex.cxx
atomsel/vmd.cxx
atomsel/vmd_keyword.cxx
atomsel/VmdLexer.c
atomsel/VmdParser.c
atomsel/within_predicate.cxx
'''))

env.Alias('install', env.Install('$PREFIX/include/msys', Glob("*.hxx")))

Export("libmsys")
